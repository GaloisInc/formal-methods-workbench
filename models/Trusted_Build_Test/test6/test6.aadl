-- AADL Test 6: active -> active threads using array read/write.
-- Also tests 'fan out' dispatch to multiple active threads on send.

package test6
public 
   with Base_Types;
   with TB_SYS;   
   with Data_Model;



data a_array
  
end a_array;


data implementation a_array.impl
  properties
    Data_Model::Data_Representation => Array;
    Data_Model::Base_Type => (classifier (Base_Types::Unsigned_32));
    Data_Model::Dimension => (4);
end a_array.impl;

-- Example of a periodically dispatched "Active" thread.   
thread sender
	features
		Output1: out event data port a_array.impl {
	      TB_SYS::CommPrim_Source_Text => "ping_Output1";			
		};
	
	properties 
	    Dispatch_Protocol => Periodic;
	    TB_SYS::Thread_Type => Active ;
	    Period => 1000 ms;
		Source_Text => ("user_code/user_sender.c");
	    TB_SYS::Compute_Entrypoint_Source_Text => ("periodic_ping");
	    Priority => 10;
	    Stack_Size => 256 bytes;
	    Compute_Execution_Time => 10 us .. 50 us;
	    TB_SYS::Sends_Events_To => "{{1 Output1}}";
end sender ; 

-- Example of a periodically dispatched "Active" thread.   
thread receiver
	features
		Input1: in event data port a_array.impl {
	      TB_SYS::Compute_Entrypoint_Source_Text => ("ping_received1");
		  Queue_Size => 8;
		}; 
		
	properties 
	    Dispatch_Protocol => Periodic;
	    TB_SYS::Thread_Type => Active ;
	    Period => 1000 ms;
		Source_Text => ("user_code/user_receiver.c");
	    TB_SYS::Compute_Entrypoint_Source_Text => ("periodic_ping1");
	    Priority => 8;
	    Stack_Size => 256 bytes;
	    Compute_Execution_Time => 10 us .. 50 us;
	    TB_SYS::Sends_Events_To => "{{}}";
end receiver ; 

-- Example of a periodically dispatched "Active" thread.   
thread receiver2
	features
		Input1: in event data port a_array.impl {
	      TB_SYS::Compute_Entrypoint_Source_Text => ("ping_received2");
		  Queue_Size => 8;
		}; 
		
	properties 
	    Dispatch_Protocol => Periodic;
	    TB_SYS::Thread_Type => Active ;
	    Period => 1000 ms;
		Source_Text => ("user_code/user_receiver2.c");
	    TB_SYS::Compute_Entrypoint_Source_Text => ("periodic_ping2");
	    Priority => 8;
	    Stack_Size => 256 bytes;
	    Compute_Execution_Time => 10 us .. 50 us;
	    TB_SYS::Sends_Events_To => "{{}}";
end receiver2 ; 

-- Example of an aperiodically dispatched "passive" thread.   
thread receiver3
	features
		Input1: in event data port a_array.impl {
	      TB_SYS::Compute_Entrypoint_Source_Text => ("ping_received3");
		  Source_Text => ("user_code/user_receiver3.c");
	      TB_SYS::Sends_Events_To => "{{}}";
		}; 

				
	properties 
	    Dispatch_Protocol => Sporadic;
	    Period => 1000 ms;
		Source_Text => ("user_code/user_receiver3.c");
	    Priority => 11;
	    Stack_Size => 16384 bytes;
	    Compute_Execution_Time => 10 us .. 50 us;
	    TB_SYS::Sends_Events_To => "{{}}";
end receiver3 ; 

process proc

end proc;

process implementation proc.Impl
	subcomponents
		s:  thread sender;
		r:  thread receiver;
		r2: thread receiver2;
		r3: thread receiver3;
	connections
		s_to_r : port s.Output1 -> r.Input1;
		s_to_r2 : port s.Output1 -> r2.Input1;
		s_to_r3 : port s.Output1 -> r3.Input1;
end proc.Impl;

system test
end test;

system implementation test.camkes
	subcomponents
		tl: process proc.Impl;
	properties
		TB_SYS::OS => CAmkES;
		TB_SYS::HW => QEMU;
end test.camkes;

system implementation test.echronos
	subcomponents
		tl: process proc.Impl;
	properties
		TB_SYS::OS => echronos;
		TB_SYS::HW => QEMU;
end test.echronos;

system implementation test.vxworks
	subcomponents
		tl: process proc.Impl;
	properties
		TB_SYS::OS => VxWorks;
		TB_SYS::HW => QEMU;
end test.vxworks;

system implementation test.linux
	subcomponents
		tl: process proc.Impl;
	properties
		TB_SYS::OS => linux;
		TB_SYS::HW => QEMU;
end test.linux;


end test6;